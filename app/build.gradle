buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}


android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    defaultConfig {
        applicationId "rom.stalker"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 6
        versionName "0.6"
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent, "afh-browser_beta_"+versionCode+".apk")
        }
    }

    signingConfigs {
        release {
            storeFile file("../extras/keystore/keystore.jks")
            storePassword System.getenv("KEYSTORE_PASSWD")
            keyAlias "stalker"
            keyPassword System.getenv("KEY_PASSWD")
         }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            shrinkResources false
            buildConfigField("boolean", "PLAY_COMPATIBLE", "true")
        }
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            buildConfigField("boolean", "PLAY_COMPATIBLE", "true")
        }
    }
    lintOptions {
        disable  'InflateParams','StringFormatInvalid','Overdraw','UnusedResources','GoogleAppIndexingWarning','RelativeOverlap','IconMissingDensityFolder','InvalidPackage'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // support libraries
    compile 'com.android.support:cardview-v7:25.0.0'
    compile 'com.android.support:design:25.0.0'
    compile 'com.android.support:appcompat-v7:25.0.0'
    compile 'com.android.support:recyclerview-v7:25.0.0'
    compile 'com.android.support:customtabs:25.0.0'

    // networking
    compile 'com.android.volley:volley:1.0.0'
    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.okhttp3:logging-interceptor:3.2.0'

    // network response parsing
    compile 'com.google.code.gson:gson:2.6.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.2'

    // ui
    compile 'com.baoyz.pullrefreshlayout:library:1.2.0'
    compile('com.mikepenz:materialdrawer:5.7.0@aar') {
        transitive = true
    }
    compile('de.psdev.licensesdialog:licensesdialog:1.8.1')
    compile('com.mikepenz:fastadapter:2.1.5@aar') {
        transitive = true
    }
    compile 'com.mikepenz:fastadapter-commons:2.1.0@aar'
    compile 'com.mikepenz.thirdparty:stickyheadersrecyclerview:0.4.4-SNAPSHOT@aar'
    compile 'com.turingtechnologies.materialscrollbar:lib:10.0.1'

    // dialogs
    compile 'com.github.javiersantos:BottomDialogs:1.2.1'
    compile 'com.afollestad.material-dialogs:core:0.9.1.0'
    compile 'com.afollestad.material-dialogs:commons:0.9.1.0'
    compile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true;
    }

    // theming
    compile('com.github.garretyoder:app-theme-engine:1.5@aar') {
        transitive = true
    }

    // updates
    compile 'com.github.javiersantos:AppUpdater:2.5'

}

tasks.whenTaskAdded { task ->
    if (task.name.equals("lint")) {
        task.enabled = false
    }
}
